group 'spark-java-ballon-stats'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    provided
    compile.extendsFrom provided
}

dependencies {

    compileOnly group: 'org.projectlombok', name:'lombok', version:'1.16.12'

    compile group: 'com.beust', name: 'jcommander', version: '1.69'
    provided group: 'org.apache.spark', name: 'spark-core_2.11', version: '2.1.0'
    provided group: 'org.apache.hadoop', name: 'hadoop-hdfs', version: '2.2.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
    manifest {
        attributes('Main-Class': "vincentg.driver.Main")
    }

    baseName = archivesBaseName
    zip64 = true

    from {
        (configurations.compile - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}

task sparkJar(type: Jar) {
    manifest {
        attributes('Main-Class': "vincentg.driver.Main")
    }

    baseName = archivesBaseName +"-sparkJar"
    zip64 = true

    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    with jar
}
